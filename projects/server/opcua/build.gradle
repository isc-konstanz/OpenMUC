def projectName = "OpenMUC Server - OPC UA"
def projectDescription = "OPC UA server for the OpenMUC framework."

def miloVersion = '0.3.6'

configurations.create('embed')

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':openmuc-server')
    implementation group: 'org.eclipse.milo', name: 'sdk-server', version: miloVersion
    embed          group: 'org.eclipse.milo', name: 'sdk-server', version: miloVersion
}

def embed = configurations.embed.findAll {
  it.getName().matches("(stack|sdk|netty|bc).*")
}

jar {
    bnd('Bundle-Name':        projectName,
        'Bundle-Description': projectDescription,
        'Bundle-ClassPath':   '.,' + embed.collect { 'lib/'+it.getName() }.join(','),
        'Import-Package':     '!org.eclipse.*,' +
                              '!io.netty.*,' +
                              '!org.jboss.*,' +
                              '!org.bouncycastle.*,' +
                              '!org.apache.log*,' +
                              '!org.apache.commons.log*,' +
                              '!com.google.protobuf.nano.*,' +
                              '!org.conscrypt.*,' +
                              '!lzma.sdk.*,' +
                              '!net.jpountz.*,' +
                              '!com.jcraft.*,' +
                              '!com.ning.*,' +
                              '!com.sun.xml*,' +
                              '!sun.security.*,' +
                              '!sun.security.util.*,' +
                              '*'+jarDefaultImportPackageVersion)

    into('lib') {
        from embed
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = projectName
                description projectDescription
            }
        }
    }
}
