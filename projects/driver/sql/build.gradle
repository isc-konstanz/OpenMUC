project.ext {
    projectName = "OpenMUC Driver - SQL"
    projectDescription = "SQL driver for the OpenMUC framework."
}

def c3poVersion =  '0.9.5.4'

configurations.create('embed')

dependencies {
    implementation project(':openmuc-driver')
    implementation group: 'com.mchange', name: 'c3p0', version: c3poVersion
    embed          group: 'com.mchange', name: 'c3p0', version: c3poVersion
}

def embed = configurations.embed.findAll {
    !it.getName().contains("protobuf")
}

jar {
    into('lib') {
        from embed
    }
    manifest {
        attributes('Bundle-Name':        projectName,
                   'Bundle-Description': projectDescription,
                   'Bundle-ClassPath': '.,' + embed.collect { 'lib/'+it.getName() }.join(','),
                   'Import-Package':    '!com.mchange.*,' +
                                        '!org.jboss.*,' +
                                        '!org.apache.log*,' +
                                        '!com.typesafe.*,' +
                                        '!javax.swing.*,' +
                                        'com.mysql.cj.jdbc,' +
                                        '*' + jarDefaultImportPackageVersion,
                   'Export-Package':    '')
    }
}
