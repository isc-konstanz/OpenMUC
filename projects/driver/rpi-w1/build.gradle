
def projectName = "OpenMUC Driver - Raspberry Pi 1-Wire"
def projectDescription = "Raspberry Pi 1-Wire driver for the OpenMUC framework."

configurations.create('embed')
configurations.embed.transitive = false;

def pi4jVersion = '1.2'

dependencies {
    implementation project(':openmuc-driver')

    implementation group: 'com.pi4j', name: 'pi4j-core', version: pi4jVersion

    implementation group: 'com.pi4j', name: 'pi4j-device', version: pi4jVersion
    embed          group: 'com.pi4j', name: 'pi4j-device', version: pi4jVersion

    implementation group: 'com.pi4j', name: 'pi4j-gpio-extension', version: pi4jVersion
    embed          group: 'com.pi4j', name: 'pi4j-gpio-extension', version: pi4jVersion
}

jar {
    bnd('Bundle-Name':        projectName,
        'Bundle-Description': projectDescription,
        'Bundle-ClassPath':   '.,' + configurations.embed.collect { 'lib/'+it.getName() }.join(','),
        'Import-Package':     '!com.pi4j.device.*,' +
                              '!com.pi4j.component.*,' +
                              '!com.pi4j.gpio.extension.*,' +
                              '*'+jarDefaultImportPackageVersion)

    into('lib') {
        from configurations.embed
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = projectName
                description projectDescription
            }
        }
    }
}
