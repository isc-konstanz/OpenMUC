
def projectName = "OpenMUC Data Logger - MySQL"
def projectDescription = "MySQL data logger for the OpenMUC framework."

def c3poVersion =  '0.9.5.4'
def mysqlVersion = '8.0.17'

configurations.create('embed')

dependencies {
  implementation project(':openmuc-core-spi')
  implementation group: 'com.mchange', name: 'c3p0',                 version: c3poVersion
  embed          group: 'com.mchange', name: 'c3p0',                 version: c3poVersion
  embed          group: 'mysql',       name: 'mysql-connector-java', version: mysqlVersion
}

def embed = configurations.embed.findAll {
  !it.getName().contains("protobuf")
}

jar {
  into('libs') {
    from embed
  }
  manifest {
    attributes('Bundle-Name':        projectName,
               'Bundle-Description': projectDescription,
               'Bundle-ClassPath': '.,' + embed.collect { 'libs/'+it.getName() }.join(','),
               'Import-Package':    '!com.mchange.*,' +
                                    '!com.mysql.*,' +
                                    '!org.jboss.*,' +
                                    '!org.apache.log*,' +
                                    '!com.typesafe.*,' +
                                    '!javax.swing.*,' +
                                    '*' + jarDefaultImportPackageVersion,
               'Export-Package':    '')
  }
}
