project.ext {
    projectName = "OpenMUC Library - MQTT Service"
    projectDescription = "MQTT service for the OpenMUC framework"
}

def hivemqVersion = '1.2.1'

configurations.create('embed')

dependencies {
    implementation project(':openmuc-core-spi')
    implementation project(':openmuc-lib-ssl')
    implementation project(':openmuc-lib-filePersistence')
    implementation group: 'com.hivemq', name: 'hivemq-mqtt-client', version: hivemqVersion
    embed group: 'com.hivemq', name: 'hivemq-mqtt-client', version: hivemqVersion

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
}

jar {
    def bundles = '.'
    configurations.embed.each {bundles += ',lib/' + it.name}
    bnd('Bundle-Name': projectName,
        'Bundle-Description': projectDescription,
        'Bundle-ClassPath': bundles,
        'Export-Package': 'org.openmuc.framework.lib.*',
        'Import-Package': 'org.openmuc.*,org.slf4j.*,org.osgi.*,javax.*,sun.misc.*'
    )

    into('lib') {
        from configurations.embed
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = projectName
                description = projectDescription
            }
        }
    }
}
