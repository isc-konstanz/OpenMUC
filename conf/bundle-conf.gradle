repositories {
  mavenLocal()
  mavenCentral()
  
  flatDir {
    dirs rootDir.getPath() + '/dependencies/pi4j'
  }
}

configurations.create("main")
configurations.create("core")
configurations.create("opt")
configurations.main.transitive = false;
configurations.core.transitive = false;
configurations.opt.transitive = false;

dependencies {
  main group: "org.apache.felix", name: "org.apache.felix.main", version: "5.6.8"

  core project(":openmuc-core-api")
  core project(":openmuc-core-spi")
  core project(":openmuc-core-datamanager")

  core project(":openmuc-datalogger-ascii")
  core project(":openmuc-datalogger-slotsdb")

  core project(":openmuc-server-restws")

  core project(":openmuc-driver-csv")

  core project(":openmuc-webui-spi")
  core project(":openmuc-webui-base")
  core project(":openmuc-webui-channelconfigurator")
  core project(":openmuc-webui-channelaccesstool")
  core project(":openmuc-webui-userconfigurator")
  core project(":openmuc-webui-dataexporter")
  core project(":openmuc-webui-dataplotter")
  core project(":openmuc-webui-mediaviewer")

  core project(":openmuc-app-simpledemo")

  //--------------------------------------------------------------------------------------------------
  // RXTX is a native interface to serial ports in java.
  //--------------------------------------------------------------------------------------------------
  core group: "org.openmuc", name: "jrxtx", version: "1.0.0"

  //--------------------------------------------------------------------------------------------------
  // Pi4J is intended to provide access to the full I/O capabilities of the Raspberry Pi platform.
  //--------------------------------------------------------------------------------------------------
  core group: "com.pi4j", name: "pi4j-core", version: "1.2-SNAPSHOT"
  core group: "com.pi4j", name: "pi4j-device", version: "1.2-SNAPSHOT"
  core group: "com.pi4j", name: "pi4j-gpio-extension", version: "1.2-SNAPSHOT"

  //--------------------------------------------------------------------------------------------------
  // The Apache Felix Gogo standard shell for OSGi (http://felix.apache.org/site/apache-felix-gogo.html)
  //--------------------------------------------------------------------------------------------------
  core group: "org.apache.felix", name: "org.apache.felix.gogo.runtime", version: "1.0.10"
  core group: "org.apache.felix", name: "org.apache.felix.gogo.command", version: "1.0.2"
  core group: "org.apache.felix", name: "org.apache.felix.gogo.shell", version: "1.0.0"

  //--------------------------------------------------------------------------------------------------
  // Adds a telnet server so that the Felix Gogo Shell can be accessed
  // using telnet clients. By default this server only listens on
  // localhost port 6666. Therefor you can on only access it from the
  // same host on which felix is running.
  //--------------------------------------------------------------------------------------------------
  core group: "org.apache.felix", name: "org.apache.felix.shell.remote", version: "1.2.0"

  //--------------------------------------------------------------------------------------------------
  // Apache Felix Service Component Runtime that implements the OSGi Declarative Services Specification
  // the OpenMUC core bundles use declarative services and thus depend on them
  //--------------------------------------------------------------------------------------------------
  core group: "org.apache.felix", name: "org.apache.felix.scr", version: "2.0.12"

  //--------------------------------------------------------------------------------------------------
  // An implementation of the OSGi HTTP Service Specification, needed by the WebUI bundles
  //--------------------------------------------------------------------------------------------------
  core group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
  core group: "org.apache.felix", name: "org.apache.felix.http.api", version: "3.0.0"
  core group: "org.apache.felix", name: "org.apache.felix.http.jetty", version: "3.4.6"

  //--------------------------------------------------------------------------------------------------
  // Implementations of the OSGi Event Admin, Configuration Admin and MetaType services, needed by jetty
  //--------------------------------------------------------------------------------------------------
  core group: "org.apache.felix", name: "org.apache.felix.eventadmin", version: "1.4.10"
  core group: "org.apache.felix", name: "org.apache.felix.configadmin", version: "1.8.16"
  core group: "org.apache.felix", name: "org.apache.felix.metatype", version: "1.1.6"

  //--------------------------------------------------------------------------------------------------
  // Message logging libraries, SLF4J is a light-weight logging API,
  // Logback is a message logger implementation that implements SLF4J
  // natively
  //--------------------------------------------------------------------------------------------------
  core group: "org.slf4j", name: "slf4j-api", version: "1.7.25"
  core group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
  core group: "ch.qos.logback", name: "logback-core", version: "1.2.3"

}

task cleanBundles(type: Delete) {
  delete fileTree(dir: "./bundle-available")
  delete fileTree(dir: "./bundle-enabled")
}

task syncCoreBundles(type: Sync) {
  from configurations.core;
  into rootDir.getPath() + "./bundle-enabled"
}

task copyFelixMain(type: Copy) {
  from configurations.main;
  into rootDir.getPath() + "./bin"
  rename (/org.apache.felix.main-.*jar/, "felix.jar")
}

task updateBundles {
  dependsOn cleanBundles
  dependsOn syncCoreBundles
  dependsOn copyFelixMain
}
